OUTPUT_FORMAT(elf64-x86-64)

ENTRY(main)

PHDRS
{
    limine_requests PT_LOAD;
    text PT_LOAD;
    rodata PT_LOAD;
    data PT_LOAD;
}

SECTIONS
{
    /* We want to be placed in the topmost 2GiB of the address space, for optimisations */
    /* and because that is what the Limine spec mandates. */
    /* Any address in this region will do, but often 0xffffffff80000000 is chosen as */
    /* that is the beginning of the region. */
    . = 0xffffffff80000000;
    _kernel_base = .;

    /* Define a section to contain the Limine requests and assign it to its own PHDR */
    .limine_requests : {
     KEEP(*(.limine_requests_start))
     KEEP(*(.limine_requests))
     KEEP(*(.limine_requests_end))
    } :limine_requests

    /* Move to the next memory page for .text */
    . = ALIGN(CONSTANT(MAXPAGESIZE));

    .text : {
     *(.text .text.*)
    } :text

    /* Move to the next memory page for .rodata */
    . = ALIGN(CONSTANT(MAXPAGESIZE));

    .rodata : {
     *(.rodata .rodata.*)
    } :rodata

    /* Add a .note.gnu.build-id output section in case a build ID flag is added to the */
    /* linker command. */
    .note.gnu.build-id : {
     *(.note.gnu.build-id)
    } :rodata

    /* Move to the next memory page for .data */
    . = ALIGN(CONSTANT(MAXPAGESIZE));


    .data : {
     *(.data .data.*)
    } :data

    /* NOTE: .bss needs to be the last thing mapped to :data, otherwise lots of */
    /* unnecessary zeros will be written to the binary. */
    /* If you need, for example, .init_array and .fini_array, those should be placed */
    /* above this. */
    .bss : {
     *(.bss .bss.*)
     *(COMMON)
    } :data

    _kernel_end = .;

    STACK_SIZE = 0x4000; /* 16 KiB stack */
    . = ALIGN(CONSTANT(MAXPAGESIZE));
    .stack : {
        . = ALIGN(16);
        _kernel_stack_bottom = .;
        . += STACK_SIZE;
        _kernel_stack_top = .;
    } :data

    FAULT_HANDLER_SIZE = 0x2000; /* 8 KiB stack for fault handlers */
    . = ALIGN(CONSTANT(MAXPAGESIZE));
    .fault_handler_stack : {
        . = ALIGN(16);
        _fault_handler_stack_bottom = .;
        . += FAULT_HANDLER_SIZE;
        _fault_handler_stack_top = .;
    } :data

    /* Discard .note.* and .eh_frame* since they may cause issues on some hosts. */
    /DISCARD/ : {
     *(.eh_frame*)
     *(.note .note.*)
    }
}